float f_mapfloat(float from_min, float from_max, float to_min, float to_max, float value) {
	return (to_min + ((to_max - to_min) / (from_max - from_min)) * (value - from_min));
}

vec3 hsv2rgb(vec3 c) {
	vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
	return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec4 floatToColor(float f) {
	vec3 color = hsv2rgb(vec3(f, 1.0, 1.0));
	return vec4(color, 1.0);
}

vec4 blendColors(vec4 bottomLayer, vec4 topLayer) {
	vec3 topColor = topLayer.rgb;
	vec3 bottomColor = bottomLayer.rgb;
	float topAlpha = topLayer.a;
	float bottomAlpha = bottomLayer.a;
	float outAlpha = topAlpha + bottomAlpha * (1.0 - topAlpha);
	vec3 outColor = (topColor * topAlpha + bottomColor * bottomAlpha * (1.0 - topAlpha)) / outAlpha;
	return vec4(outColor, outAlpha);
}

vec3 velocityToColor(vec3 velocity) {
	vec3 color;
	float speed = length(velocity);
	float normalizedSpeed = clamp(f_mapfloat(0.0, 1.0, 0.0, 1.0, speed), 0.0, 1.0);

	if (normalizedSpeed <= 0.5) {
		float t = normalizedSpeed * 2.0;
		color = mix(vec3(0.0, 0.0, 1.0), vec3(0.0, 1.0, 0.0), t);
	}
	else {
		float t = (normalizedSpeed - 0.5) * 2.0;
		color = mix(vec3(0.0, 1.0, 0.0), vec3(1.0, 0.0, 0.0), t);
	}
	return color;
}

vec3 f_temperatureToColor(float temp) {
	vec3 color;
	float t = clamp(f_mapfloat(0.0, 1.0, 1.0, 0.0, temp), 0.0, 1.0);

	if (t < 0.25) {
		// Interpolate from Red to Yellow
		float f = t / 0.25;
		color = mix(vec3(1.0, 0.0, 0.0), vec3(1.0, 1.0, 0.0), f);  // Red to Yellow
	} else if (t < 0.5) {
		// Interpolate from Yellow to Green
		float f = (t - 0.25) / 0.25;
		color = mix(vec3(1.0, 1.0, 0.0), vec3(0.0, 1.0, 0.0), f);  // Yellow to Green
	} else if (t < 0.75) {
		// Interpolate from Green to Blue
		float f = (t - 0.5) / 0.25;
		color = mix(vec3(0.0, 1.0, 0.0), vec3(0.0, 0.0, 1.0), f);  // Green to Blue
	} else {
		// Interpolate from Blue to Purple
		float f = (t - 0.75) / 0.25;
		color = mix(vec3(0.0, 0.0, 1.0), vec3(0.5, 0.0, 0.5), f);  // Blue to Purple
	}
	return color;
}

vec3 f_particleColor(in Particle particle, in float dist) {
	float distance_factor = clamp(f_mapfloat(39.0, 32.0, 0.0, 1.0, dist), 0, 1);
	switch (render_particle_color_mode) {
		case 0:  return vec3(particle.sun_intensity);
		case 1:  return vec3(particle.smoothing_radius);
		case 2:  return vec3(f_mapfloat(-8000.0, 6400.0, 0.0, 1.0, particle.height));
		case 3:  return vec3(f_mapfloat(800.0, 1020.0, 0.0, 1.0,particle.pressure));
		case 4:  return f_temperatureToColor(f_mapfloat(-25.0, 45.0, 0.0, 1.0, particle.temperature));
		case 5:  return f_temperatureToColor(f_mapfloat(-25.0, 45.0, 0.0, 1.0, particle.day_temperature));
		case 6:  return f_temperatureToColor(f_mapfloat(-25.0, 45.0, 0.0, 1.0, particle.night_temperature));
		case 7:  return vec3(f_mapfloat(0.1, 0.9, 0.0, 1.0, particle.humidity));
		case 8:  return vec3(f_mapfloat(0.0, 6.0, 0.0, 1.0, particle.water_vapor));
		case 9:  return vec3(particle.cloud_coverage);
		case 10: return vec3(f_mapfloat(0.0, 1000.0, 0.0, 1.0, particle.cloud_water_content));
		case 11: return vec3(f_mapfloat(4.0, 40.0, 0.0, 1.0, particle.cloud_particle_radius));
		case 12: return vec3(f_mapfloat(0.0, 0.5, 0.0, 1.0, particle.cloud_optical_thickness));
		case 13: return vec3(f_mapfloat(100.0, 500.0, 0.0, 1.0, particle.ozone));
		case 14: return vec3(f_mapfloat(0.0, 0.9, 0.0, 1.0, particle.albedo));
		case 15: return vec3(f_mapfloat(0.0, 16.0, 0.0, 1.0, particle.uv_index));
		case 16: return vec3(f_mapfloat(-280.0, 280.0, 0.0, 1.0, particle.net_radiation));
		case 17: return vec3(f_mapfloat(0.0, 550.0, 0.0, 1.0, particle.solar_insolation));
		case 18: return vec3(f_mapfloat(85.0, 350.0, 0.0, 1.0, particle.outgoing_longwave_radiation));
		case 19: return vec3(f_mapfloat(0.0, 425.0, 0.0, 1.0, particle.reflected_shortwave_radiation));
	}
	return vec3(1,0,1);
}

vec4 f_earthColor(in vec2 uv) {
	vec4 color;
	color = sampleTextureRgba8u(textures[render_planet_texture], uv);
	switch (render_planet_texture) {
		case 0: {
			return color;
		}
		case 3:
		case 4: {
			float mappedValue1 = f_mapfloat(-25.0, 45.0, 0.0, 1.0, -2.0);
			float mappedValue2 = f_mapfloat(-25.0, 45.0, 0.0, 1.0, 35.0);
			return vec4(f_temperatureToColor(f_mapfloat(0.0, 1.0, mappedValue1, mappedValue2, color.x)), 1.0);
		}
		case 5:
		case 6: {
			return vec4(f_temperatureToColor(color.x), 1.0);
		}
	}
	return color;
}
bool f_rayBvhIntersection(in Ray ray, in Bvh box, inout float ray_length) {
	vec3 f = (box.p_min - ray.origin) * ray.direction;
	vec3 n = (box.p_max - ray.origin) * ray.direction;

	vec3 tmax = max(f, n);
	vec3 tmin = min(f, n);

	float t1 = min(tmax.x, min(tmax.y, tmax.z));
	float t0 = max(tmin.x, max(tmin.y, tmin.z));

	if (t1 >= t0) {
		if (t0 > 0.0f) {
			ray_length = t0;
		}
		else {
			ray_length = t1;
		}
		return true;
	}
	return false;
}

bool f_rayBvhIntersection(in Ray ray, in Bvh box) {
	vec3 f = (box.p_min - ray.origin) * ray.direction;
	vec3 n = (box.p_max - ray.origin) * ray.direction;

	vec3 tmax = max(f, n);
	vec3 tmin = min(f, n);

	float t1 = min(tmax.x, min(tmax.y, tmax.z));
	float t0 = max(tmin.x, max(tmin.y, tmin.z));

	if (t1 >= t0) {
		return true;
	}
	return false;
}

bool f_raySphereIntersection(in Ray ray, in vec3 position, out float t) {
	vec3 CO = ray.origin - position;
	float a = dot(ray.direction, ray.direction);
	float b = 2.0*dot(ray.direction, CO);
	float c = dot(CO, CO) - sphere_display_radius*sphere_display_radius;
	float delta = b*b - 4.0*a*c;
	if(delta < 0.0) {
		return false;
	}
	t = (-b-sqrt(delta)) / (2.0*a);
	return true;
}

vec2 f_AtmosphereIntersection(in Ray ray, in float radius) {
	float b = dot( ray.origin, ray.direction );
	float c = dot( ray.origin, ray.origin ) - radius * radius;
	
	float d = b * b - c;
	if ( d < 0.0 ) {
		return vec2( MAX_DIST, -MAX_DIST );
	}
	d = sqrt( d );
	
	return vec2( -b - d, -b + d );
}

bool f_EarthIntersection(in Ray ray, out float t, out vec2 uv) {
	float a = dot(ray.direction, ray.direction);
	float b = 2.0*dot(ray.direction, ray.origin);
	float c = dot(ray.origin, ray.origin) - EARTH_RADIUS_SQ;
	float delta = b*b - 4.0*a*c;
	if(delta < 0.0) {
		return false;
	}
	t = (-b-sqrt(delta)) / (2.0 * a);

	vec3 intersectionPoint = ray.origin + t * ray.direction;
	vec3 normal = normalize(intersectionPoint);
	
	float axialTilt = radians(-earth_tilt);
	mat3 tiltRotation = mat3(
		vec3(cos(axialTilt), -sin(axialTilt), 0),
		vec3(sin(axialTilt), cos(axialTilt), 0),
		vec3(0, 0, 1));
	normal = tiltRotation * normal;
	
	float theta = acos(normal.y);
	float phi = atan(normal.z, normal.x);

	uv.x = 1.0 - ((phi + PI) / TWO_PI);
	uv.y = (theta) / PI;
	return true;
}

int f_visitBvh(in Ray ray, inout float result_raylength) {
	Ray bvh_ray = Ray(ray.origin, normalize(1.0 / ray.direction));

	float t_length = MAX_DIST;
	float t_dist = MAX_DIST;

	int closest_particle = -1;
	int stack[64];
	int stack_index = 0;
	stack[stack_index++] = 0;

	// TODO implement closest BVH First
	while (stack_index > 0) {
		--stack_index;

		int currentNode = stack[stack_index];
		Bvh node = bvh_nodes[currentNode];

		if (!f_rayBvhIntersection(bvh_ray, node)) {
			continue;
		}
		if (node.particle_end > 0) { // Leaf
			for (uint i = node.particle_start; i < node.particle_end; ++i) {
				if (f_raySphereIntersection(ray, particles[i].position, t_dist)) {
					if (t_dist < t_length) {
						t_length = t_dist;
						result_raylength = t_dist;
						closest_particle = int(i);
					}
				}
			}
		}
		else {
			for (int i = 0; i < 8; ++i) {
				if (i < 4) {
					int childIndex = node.pointers_a[i];
					if (childIndex >= 0) {
						stack[stack_index++] = childIndex;
					}
				}
				else {
					int childIndex = node.pointers_b[i - 4];
					if (childIndex >= 0) {
						stack[stack_index++] = childIndex;
					}
				}
			}
		}
	}
	return closest_particle;
}